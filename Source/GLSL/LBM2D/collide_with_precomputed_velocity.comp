#<compute shader>

#version 460 core

#define D2Q9 0
#define D3Q15 1
#define D3Q19 2
#define D3Q27 3

#define fp16 0
#define fp32 1

#define floating_point_accuracy fp32
#define velocity_set D2Q9

#define boundry_count 1
#define bits_per_boundry 0

#if velocity_set == D2Q9
	#define volume_dimentionality 2
	#define velocity_count 9
#elif velocity_set == D3Q15
	#define volume_dimentionality 3
	#define velocity_count 15
#elif velocity_set == D3Q19
	#define volume_dimentionality 3
	#define velocity_count 19
#elif velocity_set == D3Q27
	#define volume_dimentionality 3
	#define velocity_count 27
#else
	#error "velocity set is not supported"
#endif

#if floating_point_accuracy == fp32
#else
	#error "other floating point systems than fp32 aren't supported"
#endif

#define D2Q5 4
#define D3Q7 5

#define thermal_flow 1
#define velocity_set_thermal D2Q5

#if velocity_set_thermal == D2Q5
	#define thermal_velocity_count 5
#elif velocity_set_thermal == D3Q7
	#define thermal_velocity_count 7
#else
	#error "thermal_velocity_set is not supported"
#endif

#define multiphase_flow 1

#define id gl_GlobalInvocationID

layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;


layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

layout(std140, binding = 0) uniform velocity_set_buffer {
	vec4[velocity_count] lattice_velocity_set;
};

layout(std430, binding = 1) buffer lattice_buffer_source {
	float[] lattice_source;
};

layout(std430, binding = 2) buffer lattice_buffer_target {
	float[] lattice_target;
};

#if boundry_count > 1
layout(std430, binding = 3) buffer boundries_buffer {
	uint[] boundries;
};

layout(std430, binding = 4) buffer objects_buffer {
	vec4[] objects;
};
#endif

#if forcing_scheme == 1
#if constant_force == 0
layout(std430, binding = 5) buffer forces_buffer {
	vec4[] forces;
};
#else 
uniform vec3 force_constant;
#endif	// constant_force 
#endif	// forcing_scheme

#if thermal_flow == 1
layout(std140, binding = 6) uniform thermal_velocity_set_buffer {
	vec4[thermal_velocity_count] thermal_lattice_velocity_set;
};

layout(std430, binding = 7) buffer thermal_lattice_buffer_source {
	float[] thermal_lattice_source;
};

layout(std430, binding = 8) buffer thermal_lattice_buffer_target {
	float[] thermal_lattice_target;
};
#endif

#define velocity_field_dimentionality 4
layout(std430, binding = 9) buffer velocity_buffer {
	float[] velocities;
};

uniform ivec2 lattice_resolution;
uniform float lattice_speed_of_sound;
uniform float relaxation_time;
uniform int object_count;

#if thermal_flow == 1
uniform float thermal_lattice_speed_of_sound;
uniform float thermal_relaxation_time;
uniform float thermal_expension_coefficient;
#endif

#if multiphase_flow == 1
uniform float intermolecular_interaction_strength;
#endif

//////////////////////////////////////////
// dennsity and velocity field functionss

vec3 get_velocity_field(ivec2 pixel_coord){
	return vec3 (
		velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 0],
		velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 1],
		velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 2]
	);
}

void set_velocity_field(ivec2 pixel_coord, vec3 value){
	velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 0] = value.x;
	velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 1] = value.y;
	velocities[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_field_dimentionality + 2] = value.z;
}

//////////////////////////////////////////
// momentum density lattice functions

int get_inverse_lattice_population_id_index(int lattice_velocity_index){
	if (lattice_velocity_index == 0 || lattice_velocity_index >= velocity_count)
		return 0;

	bool is_odd = lattice_velocity_index % 2 == 1;
	return is_odd ? lattice_velocity_index + 1 : lattice_velocity_index - 1;
}

float get_lattice_source(ivec2 pixel_coord, int velocity_index){
	return lattice_source[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_count + velocity_index];
}

void set_lattice_source(ivec2 pixel_coord, int velocity_index, float value){
	lattice_source[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_count + velocity_index] = value;
}

float get_lattice_target(ivec2 pixel_coord, int velocity_index){
	return lattice_target[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_count + velocity_index];
}

void set_lattice_target(ivec2 pixel_coord, int velocity_index, float value){
	lattice_target[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * velocity_count + velocity_index] = value;
}

vec3 get_lattice_velocity(int velocity_index){
	return lattice_velocity_set[velocity_index].xyz;
}

float get_lattice_weight(int velocity_index){
	return lattice_velocity_set[velocity_index].w;
}

float compute_density(ivec2 pixel_coord){
	float density = 0;
	for (int i = 0; i < velocity_count; i++)
		density += get_lattice_source(pixel_coord, i);
	return density;
}

vec3 compute_velocity(ivec2 pixel_coord, float density){
	vec3 velocity = vec3(0);
	for (int i = 0; i < velocity_count; i++){
		velocity += get_lattice_source(pixel_coord, i) * get_lattice_velocity(i);
	}
	return velocity / density;
}

//////////////////////////////////////////
// boundry subsystem

uint get_boundry(ivec2 pixel_coord) {
#if boundry_count > 1

	uint voxel_id = pixel_coord.y * lattice_resolution.x + pixel_coord.x;
	uint bits_begin = voxel_id * bits_per_boundry;
			
	uint dword_offset = bits_begin / 32;
	uint subdword_offset_in_bits = bits_begin % 32;
	
	uint bitmask = (1 << bits_per_boundry) - 1;
	uint boundry = boundries[dword_offset] & (bitmask << subdword_offset_in_bits);
	boundry = boundry >> subdword_offset_in_bits;
		
	return boundry;
#else
	return 0;
#endif
}

struct object_info_s {
	vec3 velocity;
	vec3 center_of_mass;
	vec3 velocity_angular;
	float temperature;
	float effective_density;
};

object_info_s get_object_info(int object_id){
#if boundry_count > 1
	object_info_s result;
	result.temperature		 =	objects[object_id * 3 + 0].w;
	result.velocity			 =	objects[object_id * 3 + 0].xyz;
	result.center_of_mass	 =	objects[object_id * 3 + 1].xyz;
	result.velocity_angular  =	objects[object_id * 3 + 2].xyz;
	result.effective_density =	objects[object_id * 3 + 2].w;
	return result;
#else 
	object_info_s result;
	result.temperature		 =	0;
	result.velocity			 =	vec3(0);
	result.center_of_mass	 =	vec3(0);
	result.velocity_angular  =	vec3(0);
	result.effective_density =	0;
	return result;
#endif
}


//////////////////////////////////////////
// force subsystem
vec3 get_force(ivec2 pixel_coord){
	#if forcing_scheme == 1
		#if constant_force == 1
			return force_constant;
		#else
			uint voxel_id = pixel_coord.y * lattice_resolution.x + pixel_coord.x;
			vec3 force = forces[voxel_id].xyz;
			return force;
		#endif
	#else
		return vec3(0);
	#endif
}


//////////////////////////////////////////
// thermal flow subsystem

#if thermal_flow == 1
int get_inverse_lattice_population_id_index_thermal(int thermal_lattice_velocity_index){
	if (thermal_lattice_velocity_index == 0 || thermal_lattice_velocity_index >= thermal_velocity_count)
		return 0;

	bool is_odd = thermal_lattice_velocity_index % 2 == 1;
	return is_odd ? thermal_lattice_velocity_index + 1 : thermal_lattice_velocity_index - 1;
}

float get_lattice_source_thermal(ivec2 pixel_coord, int thermal_velocity_index){
	return thermal_lattice_source[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * thermal_velocity_count + thermal_velocity_index];
}

void set_lattice_source_thermal(ivec2 pixel_coord, int thermal_velocity_index, float value){
	thermal_lattice_source[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * thermal_velocity_count + thermal_velocity_index] = value;
}

float get_lattice_target_thermal(ivec2 pixel_coord, int thermal_velocity_index){
	return thermal_lattice_target[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * thermal_velocity_count + thermal_velocity_index];
}

void set_lattice_target_thermal(ivec2 pixel_coord, int thermal_velocity_index, float value){
	thermal_lattice_target[(pixel_coord.y * lattice_resolution.x + pixel_coord.x) * thermal_velocity_count + thermal_velocity_index] = value;
}

vec3 get_lattice_velocity_thermal(int thermal_velocity_index){
	return thermal_lattice_velocity_set[thermal_velocity_index].xyz;
}

float get_lattice_weight_thermal(int thermal_velocity_index){
	return thermal_lattice_velocity_set[thermal_velocity_index].w;
}

float compute_temperature(ivec2 pixel_coord){
	float temperature = 0;
	for (int i = 0; i < thermal_velocity_count; i++)
		temperature += get_lattice_source_thermal(pixel_coord, i);
	return temperature;
}
#endif

//////////////////////////////////////////
// multiphase flow subsystem

float get_density_pseudopotential(float density){
	float rho0 = 1;
	//return density;
	return rho0 * (1 - exp(-density/rho0));
}

ivec2 positive_modulo(ivec2 i, ivec2 n){
	return ivec2(mod((mod(i, n) + n), n));
}

vec3 compute_molecular_interaction_force(ivec2 pixel_coord){
	#if multiphase_flow == 1

	vec3 interaction_force = vec3(0);
	for (int population_id = 1; population_id < velocity_count; population_id++){
		
		vec3 c = get_lattice_velocity(population_id);
		float w = get_lattice_weight(population_id);

		ivec2 neighbour_coord = pixel_coord + ivec2(c.xy);
		neighbour_coord = positive_modulo(neighbour_coord, lattice_resolution);

		float neighbour_density = compute_density(neighbour_coord);
		float neighbour_psudopotantial = get_density_pseudopotential(neighbour_density);
		
		float psi = neighbour_psudopotantial;

		interaction_force += w * psi * c;
	}

	float self_density = compute_density(pixel_coord);
	float self_psi = get_density_pseudopotential(self_density);
	float G = intermolecular_interaction_strength;
	
	interaction_force *= -self_psi * G;

	return interaction_force;
	#else
	return vec3(0);
	#endif
}

//////////////////////////////////////////
// collision physics

float compute_equilibrium(float density, vec3 velocity, int population_id){
	float p = density;
	vec3 u = velocity;
	float w = get_lattice_weight(population_id);
	vec3 ci = get_lattice_velocity(population_id);
	float cs = lattice_speed_of_sound;

	float ci_dot_u = dot(ci, u);
	float ci_dot_u2 = pow(ci_dot_u, 2);
	float u_dot_u = dot(u, u);
	float cs2 = pow(cs, 2);
	float cs4 = pow(cs, 4);

	float equilibrium = w*p*(
		1 + ci_dot_u/cs2 + ci_dot_u2/2/cs4 - u_dot_u/2/cs2
	); 
	return equilibrium;
}

float compute_source_term(int lattice_velocity_index, vec3 force, vec3 velocity){
#if forcing_scheme == 1 || multiphase_flow == 1 || thermal_flow == 1
	float tau = relaxation_time;
	float dt = 1;
	float w = get_lattice_weight(lattice_velocity_index);
	vec3 c = get_lattice_velocity(lattice_velocity_index);
	float cs = lattice_speed_of_sound;
	float cs2 = cs*cs;
	float cs4 = cs2*cs2;
	vec3 f = force;
	vec3 u = velocity;

	return (1 - dt/(2*tau))*w*(dot(f, c)/cs2 + (dot(f,c)*dot(u,c)/cs4 - dot(f, u)/cs4));

#else
	return 0;
#endif
}

float compute_bgk_collision_term(float current_velocity, float equilibrium){
	return - (current_velocity - equilibrium) / relaxation_time;
}

//////////////////////////////////////////
// collision physics thermal
#if thermal_flow == 1
float compute_equilibrium_thermal(float temperature, vec3 velocity, int thermal_population_id){
	float t = temperature;
	vec3 u = velocity;
	float w = get_lattice_weight_thermal(thermal_population_id);
	vec3 ci = get_lattice_velocity_thermal(thermal_population_id);
	float cs = thermal_lattice_speed_of_sound;

	float ci_dot_u = dot(ci, u);
	float ci_dot_u2 = pow(ci_dot_u, 2);
	float u_dot_u = dot(u, u);
	float cs2 = pow(cs, 2);
	float cs4 = pow(cs, 4);

	float equilibrium = w*t*(
		1 + ci_dot_u/cs2 + ci_dot_u2/2.0/cs4 - u_dot_u/2.0/cs2
	); 
	return equilibrium;
}

//float compute_source_term_thermal(int thermal_lattice_velocity_index, vec3 force, vec3 velocity){
//#if forcing_scheme == 1
//	float tau = thermal_relaxation_time;
//	float dt = 1;
//	float w = get_lattice_weight_thermal(thermal_lattice_velocity_index);
//	vec3 c = get_lattice_velocity_thermal(thermal_lattice_velocity_index);
//	float cs = thermal_lattice_speed_of_sound;
//	float cs2 = pow(thermal_lattice_speed_of_sound, 2);
//	vec3 f = force;
//	vec3 u = velocity;
//
//	return (1 - dt/(2*tau))*w*(dot(f, c)*3 + (dot(f, c)*dot(c,u)*9 + dot(f, u)*3));
//
//#else
//	return 0;
//#endif
//}

float compute_bgk_collision_term_thermal(float current_velocity, float thermal_equilibrium){
	return - (current_velocity - thermal_equilibrium) / (thermal_relaxation_time);
}
#endif

void main(){
	
	if (int(id.x) >= lattice_resolution.x * lattice_resolution.y)
		return;

	int pixel_id = int(id.x);
	ivec2 pixel_coord = ivec2(pixel_id % lattice_resolution.x, pixel_id / lattice_resolution.x);

	#if boundry_count > 1
	uint boundry_id = get_boundry(pixel_coord);
	//if (boundry_id != 0)
	//	return;
	#endif

	float density = compute_density(pixel_coord);
	//if (density == 0)
	//	return;

	vec3 force = get_force(pixel_coord);
	
	#if thermal_flow == 1
	float temperature = compute_temperature(pixel_coord);
	float referance_density = 1;
	float referance_temperature = 1;
	vec3 buoyancy_force = -thermal_expension_coefficient * referance_density * (temperature - referance_temperature) * force;
	force += buoyancy_force;
	#endif

	#if multiphase_flow == 1
	force += compute_molecular_interaction_force(pixel_coord);
	#endif

	vec3 velocity = get_velocity_field(pixel_coord) + force/(2*density);

	for (int population_id = 0; population_id < velocity_count; population_id++){
		float weight = get_lattice_weight(population_id);
		float equilibrium_velocity = compute_equilibrium(density, velocity, population_id);
		lattice_target[pixel_id * velocity_count + population_id] = 
			get_lattice_source(pixel_coord, population_id) + 
			compute_bgk_collision_term(get_lattice_source(pixel_coord, population_id), equilibrium_velocity) + 
			compute_source_term(population_id, force, velocity);
	}
}